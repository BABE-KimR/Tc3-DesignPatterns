<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_EventSubscriber" Id="{d4dce0f2-3ded-43b2-8491-9014ab19c774}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_EventSubscriber IMPLEMENTS I_Observer
VAR_INPUT
	eventPublisher					: I_EventPublisher;
END_VAR
VAR
	_verbose						: BOOL := TRUE;
	_event							: INT;
	// Register after online change 
	registeredAtPublisher			: BOOL;
	
	breinit 						: BOOL; // reinit test marker
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Is there a valid publisher attached? 
IF eventPublisher=0	THEN
	RETURN;
END_IF

// Did we already register in the publisher?
IF NOT registeredAtPublisher	THEN
	registeredAtPublisher := eventPublisher.attach(THIS^);
END_IF 
]]></ST>
    </Implementation>
    <Property Name="event" Id="{aa9553a2-33bf-40d8-a234-28f7162b10c5}">
      <Declaration><![CDATA[// IMPLEMENTED ONLY FOR TESTING PURPOSES
PROPERTY event : int]]></Declaration>
      <Get Name="Get" Id="{231fdd1c-03f2-4591-b73d-451501ccc307}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[event := _event;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="fbreinit" Id="{981288f7-d0dd-4f4e-9719-c7816223b9f0}">
      <Declaration><![CDATA[{attribute 'call_after_online_change_slot' := '0'}
METHOD PRIVATE fbreinit

(*	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
   	THIS METHOD is force called after every online change 
   	!= the fb_reinit system call that will be called after the instance of the concerned function block has been copied 
 	(during an online change after modifications in the function block declaration)

	This is necessary since both observer en observee have references to one other
	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!	*) 
]]></Declaration>
      <Implementation>
        <ST><![CDATA[registeredAtPublisher := FALSE;
breinit:=not breinit; // flip the test marker]]></ST>
      </Implementation>
    </Method>
    <Method Name="message" Id="{e4f26ffc-57fb-4c96-b5ee-4b9c9356b25c}">
      <Declaration><![CDATA[METHOD PRIVATE message : BOOL
VAR_INPUT
	dintArg						: DINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Log message
IF _verbose  THEN
	ADSLOGDINT(
		msgCtrlMask := ADSLOG_MSGTYPE_HINT OR ADSLOG_MSGTYPE_LOG, // ADSLOG_MSGTYPE_ERROR OR ADSLOG_MSGTYPE_LOG, 
		msgFmtStr 	:= 'Subscriber 1 knows the value changed to %d',
		dintArg 	:= dintArg
		);
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="update" Id="{26657e55-00fd-4c13-80e1-21813364b2ff}">
      <Declaration><![CDATA[METHOD update : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// the publisher mentions an update, fetch the data
_event := eventPublisher.event;
message(TO_DINT(_event));
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>